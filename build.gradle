plugins {
    id 'io.franzbecker.gradle-lombok' version '1.11' apply false
}

import io.franzbecker.gradle.lombok.task.DelombokTask

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'io.franzbecker.gradle-lombok'

    version '1.0.0-SNAPSHOT'

    group 'co.aikar'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    lombok {
        version = '1.16.20'
        sha256 = ""
    }


    // Delombok Task
    task delombok(type: DelombokTask, dependsOn: compileJava) {
        ext.outputDir = file("$buildDir/delombok")
        classpath += project.configurations.compileOnly
        outputs.dir(outputDir)
        sourceSets.main.java.srcDirs.each {
            inputs.dir(it)
            args(it, "-d", outputDir)
        }
    }

    //noinspection GroovyAssignabilityCheck
    task sourcesJar(type: Jar, dependsOn: classes) {
        dependsOn delombok
        classifier = 'sources'

        from sourceSets.main.allSource
    }
    // Javadoc Task
    javadoc {
        dependsOn delombok

        source = file( "${buildDir}/${delombok.name}" )
        failOnError = true
    }

    //noinspection GroovyAssignabilityCheck
    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    task deploy() {
        dependsOn publish
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifact sourcesJar
                artifact javadocJar
                from components.java
            }
        }
        repositories {
            maven {
                url 'https://repo.aikar.co/content/repositories/aikar-snapshots/'
                if (project.hasProperty('aikarRepoUser') && project.hasProperty('aikarRepoPass')) {
                    credentials {
                        username = aikarRepoUser
                        password = aikarRepoPass
                    }
                }
            }
        }
    }
    dependencies {
        compileOnly group: 'org.jetbrains', name: 'annotations', version: '15.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "aikar"
            url = "https://repo.aikar.co/content/groups/aikar/"
        }
    }
}
